@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
find_dependency(Boost 1.64 COMPONENTS system date_time)
find_dependency(Threads)

if(KINGKONG_ENABLE_COMPRESSION)
  find_dependency(ZLIB)
endif()

if(KINGKONG_ENABLE_SSL)
  find_dependency(OpenSSL)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/KingKongTargets.cmake")
check_required_components("@PROJECT_NAME@")

get_target_property(_KINGKONG_ILL KingKong::KingKong INTERFACE_LINK_LIBRARIES)
get_target_property(_KINGKONG_ICD KingKong::KingKong INTERFACE_COMPILE_DEFINITIONS)

list(REMOVE_ITEM _KINGKONG_ILL "ZLIB::ZLIB" "OpenSSL::SSL")
list(REMOVE_ITEM _KINGKONG_ICD "KINGKONG_ENABLE_SSL" "KINGKONG_ENABLE_COMPRESSION")

if(KINGKONG_ENABLE_COMPRESSION)
  list(APPEND _KINGKONG_ILL "ZLIB::ZLIB")
  list(APPEND _KINGKONG_ICD "KINGKONG_ENABLE_COMPRESSION")
endif()

if(KINGKONG_ENABLE_SSL)
  list(APPEND _KINGKONG_ILL "OpenSSL::SSL")
  list(APPEND _KINGKONG_ICD "KINGKONG_ENABLE_SSL")
endif()

set_target_properties(KingKong::KingKong PROPERTIES
  INTERFACE_COMPILE_DEFINITIONS "${_KINGKONG_ICD}"
  INTERFACE_LINK_LIBRARIES "${_KINGKONG_ILL}"
)